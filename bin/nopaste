#!/usr/bin/env perl
use strict;
use warnings;

use WWW::Mechanize;
use Getopt::Long;

my $nick = $ENV{USER};
my $channel = "";
my $summary = "";
my $paste;
my $host = "paste.husk.org";

sub usage {
    print <<"END_USAGE";
Usage: $0 <options>
    Reads paste from STDIN

    Options:

    --nick=NICK       Paste as NICK (default \$USER)
    --summary=        Set the paste summary
    --channel=        Paste to a given channel
    --host=           Use a different host other than paste.husk.org
    
END_USAGE

    exit(-1);
}

GetOptions("nick=s"     => \$nick,
           "summary=s"  => \$summary,
           "channel=s"  => \$channel,
           "host=s"     => \$host)
or usage();

$paste = do { local $/; <> };

my $mech = WWW::Mechanize->new();

$mech->get("http://$host");

$mech->submit_form(
    form_number   => 1,
    fields         => {
        channel    => $channel,
        nick       => $nick,
        summary    => $summary,
        paste      => $paste,
       }
   );

my $link = $mech->find_link( url_regex => qr{\Q$host\E/(?:\d+)} );

if (!$link) {
    warn("Couldn't find paste link.");
} else {
    print "Pasted to: " . $link->url, "\n";
}
